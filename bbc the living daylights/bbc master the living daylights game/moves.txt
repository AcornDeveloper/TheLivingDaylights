   10 REM MOVES
   20 *K.0MO.7|MVDU14|ML.830,|M
   30 *K.1RUN|M
   40 *K.2REN.|M
   50 *K.3P.~P%|M
   60 *K.10PA.=&5800|MO.|M*FX12,2|M
   70 *TV255
   80 roads=&46
   90 speed=&48
  100 chose=&49
  110 decid=&4B
  120 pipex=&4C
  130 pipey=&4E
  140 tempa=&70
  150 tempx=&71
  160 tempy=&72
  170 tempz=&73
  180 indir=&74
  190 stick=&76
  200 fires=&78
  210 keyys=&7A
  220 level=&7D
  230 wepon=&7E
  240 runns=&80
  250 foods=&81
  260 falen=&8F
  270 explo=&95
  280 crosx=&96
  290 crosy=&97
  300 enlar=&9B
  310 INDIR=&1203
  320 METAL=&120C
  330 DIABL=&1553
  340 EIGHT=&1A73
  350 NOISE=&1C46
  360 RANDY=&1EC0
  370 OSB=&FFF4
  380  
  390 rolls=&97
  400 zzzzz=&9E
  410 cartn=&B6
  420 colon=&B7
  430 xxxxx=&BD
  440 FORA%=0TO2STEP2
  450 P%=&2A80
  460 [OPTA%
  470 .CBOND JMPA
  480 .SIGHT JMPB
  490 .PIPEA JMPC
  500 .PIPEB JMPD
  510 .PIPEC JMPE
  520 .HAIRS JMPF
  530 .CROSS JMPG
  540 .EATEN JMPH
  550 .PIPED JMPI
  560 .keybd
  570 EQUBzzzzz:EQUBxxxxx
  580 EQUBcolon:EQUBrolls
  590 .value
  600 EQUB0:EQUB&FF:EQUB&0:EQUB&FF
  610 .A
  620 LDY#3:LDAkeyys:BNEother
  630 .moves
  640 LDXkeybd,Y:STYtempy
  650 LDA#129:TAY:JSROSB:TYA
  660 BMIpress:LDYtempy
  670 DEY:BPLmoves
  680 LDA#&FF:RTS
  690 .other
  700 STYtempy
  710 TYA:LSRA:TAX:LDAstick,X
  720 EORvalue,Y:CMP#&D0:BCSpress
  730 LDYtempy:DEY:BPLother
  740 LDA#&FF:RTS
  750 .press
  760 LDAtempy:RTS
  770 .direx
  780 EQUB-2:EQUB2:EQUB0:EQUB0
  790 EQUB-2:EQUB2
  800 .sizes
  810 EQUD&6030300:EQUD&A060603
  820 .B
  830 JSRCBOND:BPLgotit
  840 .carry
  850 CLC:ROLrunns
  860 .relap RTS
  870 .gotit
  880 STAtempy
  890 LDAkeyys:BNEjstik
  900 LDX#cartn:JSRMETAL
  910 BMIcarry:BPLinher
  920 .jstik
  930 LDAfires:BNEcarry
  940 .inher
  950 LDArunns:AND#7:TAX
  960 LDAsizes,X:STAtempx
  970 LDXtempx:LDYtempy:LDA#0:CLC
  980 .addin
  990 ADCdirex,Y:DEX:BPLaddin:STAtempz
 1000 LDAcrosx:CLC:ADCtempz
 1010 CMP#16:BCSforwd:LDA#16
 1020 .forwd
 1030 CMP#170:BCCright:LDA#169
 1040 .right
 1050 STAenlar
 1060 LDXtempx:LDYtempy:LDA#0:CLC
 1070 .yates
 1080 ADCdirex+2,Y:DEX:BPLyates:STAtempz
 1090 LDAcrosy:CLC:ADCtempz
 1100 CMP#32:BCSwindo:LDA#32
 1110 .windo
 1120 CMP#144:BCCwinda:LDA#143
 1130 .winda
 1140 STAenlar+1:CMPcrosy:BNEremov
 1150 LDAenlar:CMPcrosx:BEQreles
 1160 .remov
 1170 JSRCROSS
 1180 LDAenlar:STAcrosx
 1190 LDAenlar+1:STAcrosy
 1200 JSRCROSS
 1210 SEC:ROLrunns:RTS
 1220 .C
 1230 LDA#&FF:STApipex:STApipex+1
 1240 .reles RTS
 1250 .D
 1260 LDAlevel:CMP#2:BNEreles
 1270 LDAroads:CMP#8:BCCupdat
 1280 LDX#1
 1290 .steel
 1300 LDApipex,X:CMP#&FF:BNEoccup
 1310 STXtempx:JSRRANDY:LDXtempx
 1320 CMP#24:BCSoccup
 1330 CPY#30:BCCoccup
 1340 CPY#140:BCSoccup
 1350 STYpipex,X:STYtempx
 1360 LDY#0:STYpipey,X:STYtempy
 1370 INY:STYtempa:JSREIGHT
 1380 LDA#4:JMPNOISE
 1390 .occup
 1400 DEX:BPLsteel
 1410 .updat
 1420 LDX#1
 1430 .metal
 1440 LDApipex,X:EOR#&FF:BEQdrops
 1450 STXexplo:JSRPIPES
 1460 LDApipey,X:CLC:ADC#3:STApipey,X
 1470 CMP#120:BCCfalin
 1480 LDA#&FF:STApipex,X
 1490 .falin
 1500 LDApipey,X:CMP#84:BCCmissd
 1510 LDApipex,X
 1520 CMP#28:BCCmissd
 1530 CMP#48:BCSmissd
 1540 LDAroads:BEQnohit
 1550 LDYchose:LDAwepon,Y
 1560 CMP#9:BEQnofal
 1570 LDA#16:JSREATEN
 1580 LDAdecid:CMP#3:BEQnohit
 1590 LDA#3:STAdecid
 1600 LDA#7:STAspeed
 1610 .nohit
 1620 LDA#12:STAfalen
 1630 .nofal
 1640 LDApipex,X:SEC:SBC#12:STApipex,X
 1650 LDA#10:JSRNOISE:LDXexplo
 1660 .missd
 1670 JSRPIPES
 1680 .drops
 1690 DEX:BPLmetal:RTS
 1700 .PIPES
 1710 LDA#1:STAtempa
 1720 LDApipex,X:CMP#&FF
 1730 BEQrelax:STAtempx
 1740 LDApipey,X:STAtempy
 1750 JSREIGHT:LDXexplo
 1760 .relax RTS
 1770 .drift EQUW&402
 1780 .E
 1790 LDX#1
 1800 .slope
 1810 LDApipex,X:CMP#&FF:BEQnoton
 1820 LDY#0
 1830 LDApipey,X:CMP#32:BCCsmall:INY
 1840 .small
 1850 LDApipex,X:SEC:SBCdrift,Y
 1860 BCSinmid:LDA#&FF
 1870 .inmid
 1880 STApipex,X
 1890 .noton
 1900 DEX:BPLslope
 1910 .invis RTS
 1920 .F
 1930 LDA#80:STAcrosx
 1940 LDA#100:STAcrosy
 1950 .G
 1960 LDXcrosx:CPX#144
 1970 BCSinvis:STXtempx
 1980 LDYcrosy:STYtempy
 1990 LDA#4:STAtempa
 2000 JMPEIGHT
 2010 .H
 2020 STAtempa:LDAfoods:SEC
 2030 SBCtempa:BCScxsix:LDA#0
 2040 .cxsix
 2050 CMP#16:BCSalrig:LDA#0
 2060 .alrig
 2070 STAfoods:RTS
 2080 .I
 2090 LDX#0:JSRPIPES
 2100 LDX#1:JMPPIPES
 2110 ]:NEXT

